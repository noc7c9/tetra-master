_list:
    @just --list

start:
    RUST_BACKTRACE=1 just watch run

watch CMD='check' +ARGS='':
    watchexec \
        --watch Cargo.toml --watch src \
        --restart --clear -- just {{CMD}} {{ARGS}}

cargo +ARGS='':
    cargo {{ARGS}}

run +ARGS='':
    cargo run --features bevy/dynamic {{ARGS}}

check +ARGS='':
    cargo clippy {{ARGS}}

build +ARGS='':
    cargo build {{ARGS}}

[linux]
build-wasm:
    trunk build --release --public-url "SED_REPLACE_MARKER"
    sed --in-place 's/\/SED_REPLACE_MARKER/./g' ./dist/index.html

[macos]
build-wasm:
    trunk build --release --public-url "SED_REPLACE_MARKER"
    sed -i  '' 's/\/SED_REPLACE_MARKER/./g' ./dist/index.html

serve-wasm: build-wasm
    cd ./dist && python3 -m http.server
